
// Simplified N-Body
class Body {
    init(x, y, z, mass) {
        this.x = x;
        this.y = y;
        this.z = z;
        this.mass = mass;
        this.vx = 0;
        this.vy = 0;
        this.vz = 0;
    }
}

func advance(bodies, dt) {
    let n = length(bodies);
    for (let i = 0; i < n; i = i + 1) {
        for (let j = i + 1; j < n; j = j + 1) {
            let b1 = bodies[i];
            let b2 = bodies[j];
            
            let dx = b1.x - b2.x;
            let dy = b1.y - b2.y;
            let dz = b1.z - b2.z;
            
            let dist_sq = dx*dx + dy*dy + dz*dz;
            let mag = dt / (dist_sq * sqrt(dist_sq));
            
            b1.vx = b1.vx - dx * b2.mass * mag;
            b1.vy = b1.vy - dy * b2.mass * mag;
            b1.vz = b1.vz - dz * b2.mass * mag;
            
            b2.vx = b2.vx + dx * b1.mass * mag;
            b2.vy = b2.vy + dy * b1.mass * mag;
            b2.vz = b2.vz + dz * b1.mass * mag;
        }
    }
    
    for (let i = 0; i < n; i = i + 1) {
        let b = bodies[i];
        b.x = b.x + b.vx * dt;
        b.y = b.y + b.vy * dt;
        b.z = b.z + b.vz * dt;
    }
}

func main() {
    let start = time();
    let bodies = [
        Body(0, 0, 0, 100),
        Body(10, 0, 0, 10),
        Body(0, 10, 0, 10),
        Body(0, 0, 10, 10)
    ];
    
    for (let i = 0; i < 1000; i = i + 1) {
        advance(bodies, 0.01);
    }
    
    print("N-Body complete");
     let elapsed = time() - start;
    print("Time: " + to_string(elapsed));
}

main();
